apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId 'pl.org.seva.navigator'
        minSdkVersion 21
        targetSdkVersion 28
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary true
        versionCode 48
        versionName '2.8.0'
        resConfigs 'en', 'pl'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        lintOptions {
            checkReleaseBuilds false
        }
    }

    /* Android Extensions plugin includes several experimental
       features such as LayoutContainer support and a Parcelable
       implementation generator. These features are not considered
       production ready yet, so you need to turn on the experimental
       mode in build.gradle in order to use them:

       https://kotlinlang.org/docs/tutorials/android-plugin.html
     */
    androidExtensions {
        experimental = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
        release.java.srcDirs += 'src/release/kotlin'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            crunchPngs true
        }
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/main.kotlin_module'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // https://androidstudio.googleblog.com/2018/04/android-studio-32-canary-11-now.html
    lintOptions {
        check 'Interoperability'
    }

    androidExtensions {
        experimental = true
    }
}

kapt {
    // https://blog.jetbrains.com/kotlin/2018/01/kotlin-1-2-20-is-out/
    useBuildCache = true
    // https://blog.jetbrains.com/kotlin/2018/03/kotlin-1-2-30-is-out/
    mapDiagnosticLocations = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Support Library
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'org.kodein.di:kodein-di-generic-jvm:5.3.0'
    implementation 'org.kodein.di:kodein-di-conf-jvm:5.3.0'
    implementation 'org.kodein.di:kodein-di-framework-android:5.0.0'
    implementation 'androidx.core:core-ktx:1.0.1'

    // Jetpack
    kapt 'androidx.room:room-compiler:2.0.0'
    implementation 'androidx.room:room-runtime:2.0.0'
    implementation 'androidx.room:room-rxjava2:2.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'android.arch.navigation:navigation-fragment-ktx:1.0.0-alpha08'
    implementation 'android.arch.navigation:navigation-ui-ktx:1.0.0-alpha08'

    // Play, Firebase
    implementation 'com.google.android.gms:play-services-maps:16.0.0'
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    implementation 'com.google.android.gms:play-services-auth:16.0.1'
    implementation 'com.google.firebase:firebase-core:16.0.6'
    implementation 'com.google.firebase:firebase-auth:16.0.5'
    implementation 'com.google.firebase:firebase-database:16.0.5'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.2@aar') {
        transitive = true
    }

    // Commons
    implementation 'commons-io:commons-io:2.5'

    // Testing
    androidTestImplementation 'org.mockito:mockito-android:2.18.3'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'
